package com.cjgammon.particlemapper
{
	import com.cjgammon.particlemapper.components.PMParticle;
	
	import flash.events.*;
	import flash.filesystem.File;
	import flash.filesystem.FileMode;
	import flash.filesystem.FileStream;

	public class PMModel
	{
		private var particleList:Array = [];
		private var idcount:int = 0;
		private var filecontents:String;
		
		public function PMModel()
		{
			
		}
		
		public function addParticle(p:PMParticle):void{
			p.id = idcount;
			particleList.push(p);
			idcount += 1;
		}
		
		public function export():void{
			trace('export');
			var saveFile:File = new File();
			saveFile.addEventListener(Event.SELECT, handle_saveFile_SELECT);
			saveFile.browseForSave("Save File As:");
		}
		
		private function handle_saveFile_SELECT(event:Event):void
		{
			var outputFile:File = File(event.currentTarget);				
			var stream:FileStream = new FileStream();
			stream.open( outputFile, FileMode.WRITE );
			filecontents = getFileContents();
			stream.writeUTFBytes(filecontents);
			stream.close();	
			stream = null;
			
			outputFile = null;
		}
		
		private function getFileContents():String{
			var str:String = "/*GENERATED BY PARTICLE MAPPER*/ \n";
			str = str.concat("/*by cjgammon http://www.cjgammon.com*/ \n\n");
			str = str.concat("var pointList=[");
			for (var i:int = 0; i < particleList.length; i += 1) {
				var particle:PMParticle = particleList[i];
				str = str.concat("[" + particle.x + ", " + particle.y + "]");
				if (i < particleList.length - 1) {
					str = str.concat(", ");
				}
			}
			str = str.concat("];");
			return str;
		}
	}
}